/PROG  _SAND_LS_G4
/ATTR
OWNER		= MNEDITOR;
COMMENT		= "";
PROG_SIZE	= 1324;
CREATE		= DATE 21-08-19  TIME 21:16:22;
MODIFIED	= DATE 21-08-19  TIME 21:16:22;
FILE_NAME	= _SAND_LS;
VERSION		= 0;
LINE_COUNT	= 49;
MEMORY_SIZE	= 1800;
PROTECT		= READ_WRITE;
TCD:  STACK_SIZE	= 0,
      TASK_PRIORITY	= 50,
      TIME_SLICE	= 0,
      BUSY_LAMP_OFF	= 0,
      ABORT_REQUEST	= 0,
      PAUSE_REQUEST	= 0;
DEFAULT_GROUP	= 1,*,*,*,*;
CONTROL_CODE	= 00000000 00000000;
/APPL
/APPL

AUTO_SINGULARITY_HEADER;
  ENABLE_SINGULARITY_AVOIDANCE   : TRUE;
/MN
   1:  UTOOL_NUM=2 ;
   2:  UFRAME_NUM=1 ;
   3:   ;
   4:  PR[30,1:SAND LEFT SIDE]=R[44:SAND LS G4]    ;
   5:J PR[30:SAND LEFT SIDE] 50% CNT10    ;
   6:  !SAND WELD #2 ;
   7:  !ABOVE WELD ;
   8:   ;
   9:  !SAND WELD ;
  10:  CALL _SANDER_ON    ;
  11:  WAIT R[11] ;
  12:   ;
  13:  !CALCULATE DOWN OFFSET ;
  14:  !FOR LEFT SIDE WELDS ;
  15:  PR[11:SAND OFFSET]=LPOS    ;
  16:  R[46:S LS HOLDER]=PR[11,3:SAND OFFSET]    ;
  17:  R[46:S LS HOLDER]=R[46:S LS HOLDER]+R[45:SAND LS DOWN]    ;
  18:   ;
  19:  OVERRIDE=R[2:S SPEED OVERIDE] ;
  20:  !DOWNWARD GRIND ;
  21:  PR[11,3:SAND OFFSET]=R[46:S LS HOLDER]    ;
  22:L PR[11:SAND OFFSET] 100mm/sec CNT R[13:SAND CNT]    ;
  23:   ;
  24:  !SAND LEFT ;
  25:  AO[1:COMPENSATION]=0 ;
  26:  AO[1:COMPENSATION]=R[17:AO1] ;
  27:  CALL _LS_S_SLEFT    ;
  28:   ;
  29:  !SAND RIGHT ;
  30:  AO[1:COMPENSATION]=0 ;
  31:  AO[1:COMPENSATION]=R[18:AO2] ;
  32:  CALL _LS_S_SRIGHT    ;
  33:   ;
  34:  !SAND 2nd LEFT ;
  35:  AO[1:COMPENSATION]=0 ;
  36:  AO[1:COMPENSATION]=R[18:AO2] ;
  37:  CALL _LS_S_2ND_SLEFT    ;
  38:   ;
  39:  !RETREAT SAND ;
  40:  CALL _LS_S_RETREAT    ;
  41:   ;
  42:  !SAFE RETREAT ;
  43:J P[1] 50% CNT50    ;
  44:   ;
  45:  !NEXT WELD ;
  46:  R[24:SAND LEFT SIDE]=R[24:SAND LEFT SIDE]+1    ;
  47:   ;
  48:  LBL[99] ;
  49:  AO[1:COMPENSATION]=0 ;
/POS
P[1]{
   GP1:
	UF : 1, UT : 2,		CONFIG : 'N U T, 0, 0, 0',
	X =   236.701  mm,	Y =    17.910  mm,	Z =  -188.108  mm,
	W =   179.058 deg,	P =     1.877 deg,	R =   -88.851 deg
};
/END
